DISABLE_MAGIC_FUNCTIONS=true

[[ -o interactive ]] || return

# If inside tmux, import BASE16_THEME from tmux's global environment
if [[ -n "$TMUX" ]]; then
  local line
  line="$(tmux show-environment -g BASE16_THEME 2>/dev/null)" || true
  if [[ "$line" == BASE16_THEME=* ]]; then
    export BASE16_THEME="${line#BASE16_THEME=}"
  fi
fi

# history
HISTFILE=$HOME/.zhistory
SAVEHIST=1000
HISTSIZE=1000
setopt SHARE_HISTORY INC_APPEND_HISTORY
setopt HIST_EXPIRE_DUPS_FIRST HIST_IGNORE_ALL_DUPS HIST_REDUCE_BLANKS
setopt HIST_IGNORE_SPACE

# keybinds
bindkey '^[[3;5~' backward-kill-word # Ctrl+Backspace
bindkey '^[OA' history-search-backward
bindkey '^[OB' history-search-forward
bindkey '^[[1;2D' backward-word
bindkey '^[[1;2C' forward-word

# tools (guarded)
command -v fasd >/dev/null && eval "$(fasd --init auto)"
command -v direnv >/dev/null && eval "$(direnv hook zsh)"

# aliases
alias ls="eza -l --group-directories-first -s type --git --git-ignore"
alias r='cd $(git rev-parse --show-toplevel)'
alias g='git'
alias k='kubectl'
alias rg="rg --hidden"
alias yt="youtube-dl"

if [[ "$(uname)" == "Linux" ]]; then
  alias fd='fdfind'
fi

mkc() { mkdir -p -- "$1" && cd -- "$1"; }

zf() {
  local dir
  dir="$(fasd -Rdl -- "${1-}" | fzf -1 -0 --no-sort +m)" || return 1
  cd -- "$dir"
}

# env
export EDITOR=nvim
export DOCKER_BUILDKIT=1
export GOPATH=$HOME/go
export GPG_TTY=$(tty)
export PYENV_ROOT="$HOME/.pyenv"
export DIRENV_LOG_FORMAT=""

typeset -U path PATH
path=(
  $HOME/.nix-profile/bin
  $HOME/.nix-profile/sbin
  $HOME/.local/bin
  $HOME/go/bin
  $HOME/.yarn/bin
  $HOME/n/bin
  $PYENV_ROOT/bin
  $path
)
export PATH

# secrets
[[ -f ~/.dotfiles/secrets/env ]] && source ~/.dotfiles/secrets/env

# nix
[[ -r "$HOME/.nix-profile/etc/profile.d/hm-session-vars.sh" ]] && source "$HOME/.nix-profile/etc/profile.d/hm-session-vars.sh"
[[ -r "$HOME/.nix-profile/etc/profile.d/nix.sh" ]] && source "$HOME/.nix-profile/etc/profile.d/nix.sh"

# ssh-add (quiet)
if [[ -S "$SSH_AUTH_SOCK" && -r ~/.ssh/github_rsa ]]; then
  ssh-add ~/.ssh/github_rsa >/dev/null 2>&1
fi

# tmux auto-attach on SSH
if [[ -z "$TMUX" && -n "$SSH_CONNECTION" ]]; then
  tmux attach -t ssh_tmux || tmux new -s ssh_tmux
fi

# Base16 Shell
BASE16_SHELL="$HOME/.config/base16-shell"
if [[ -n "$PS1" && -s "$BASE16_SHELL/profile_helper.sh" ]]; then
  source "$BASE16_SHELL/profile_helper.sh"
fi

# -- -base16-shell wrapper: export + tmux-wide refresh + refresh vim/nvim panes ---
if typeset -f set_theme >/dev/null; then
  functions[__base16_set_theme_orig]=$functions[set_theme]

  set_theme() {
    __base16_set_theme_orig "$@" || return $?

    local theme_name="${1:-${BASE16_THEME_DEFAULT:-}}"
    [[ -z "$theme_name" ]] && return 0

    export BASE16_THEME="$theme_name"

    [[ -n "$TMUX" ]] || return 0

    tmux set-environment -g BASE16_THEME "$theme_name" >/dev/null 2>&1
  }
fi
